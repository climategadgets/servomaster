dnl Process this file with autoconf to produce a configure script.

AC_REVISION($Id: configure.in,v 1.12 2002-09-14 01:34:03 vtt Exp $)dnl

dnl I'd like it to read the configure-options to allow both the global set
dnl of options, as well as the local set.

GLOBAL_OPTIONS=/usr/local/share/servomaster/configure-options

if test -f "${GLOBAL_OPTIONS}" && test -z "${GLOBAL_OPTIONS_PRESENT}" ; then
    echo "Reading global options from ${GLOBAL_OPTIONS}"
    GLOBAL_OPTIONS_PRESENT="1" $0 `cat ${GLOBAL_OPTIONS}|tr "\n" " "` $*
    exit $?
fi

LOCAL_OPTIONS=`pwd`/configure-options

if test -f "${LOCAL_OPTIONS}" && test -z "${LOCAL_OPTIONS_PRESENT}" ; then
    echo "Reading local options from ${LOCAL_OPTIONS}"
    LOCAL_OPTIONS_PRESENT="1" $0 `cat ${LOCAL_OPTIONS}|tr "\n" " "` $*
    exit $?
fi

dnl This file ensures our --srcdir is right. Later it's also used to verify
dnl the acceptable version of Java compiler and the options specific to the
dnl versions.

AC_INIT(src/java/org/freehold/servomaster/device/model/Servo.java)

VERSION_MAJOR="0"
VERSION_MINOR="4"
PATCHLEVEL="2"

AC_SUBST(VERSION_MAJOR)dnl
AC_SUBST(VERSION_MINOR)dnl
AC_SUBST(PATCHLEVEL)dnl

PACKAGE="servomaster"
VERSION="${VERSION_MAJOR}.${VERSION_MINOR}p${PATCHLEVEL}"

AC_SUBST(PACKAGE)dnl
AC_SUBST(VERSION)dnl

AC_MSG_RESULT(Configuring ${PACKAGE}-${VERSION})

AC_PREFIX_DEFAULT(/usr/local/servomaster)dnl
AC_PREREQ(2.13)dnl
AC_CONFIG_AUX_DIR(src/scripts/install)dnl

AM_INIT_AUTOMAKE(${PACKAGE}, ${VERSION})dnl

AC_PROG_TEST
AC_PROG_TR
AC_PROG_FIND
AC_PATH_PROG_VERIFY(RM,rm,$PATH)dnl
AC_PATH_PROG_VERIFY(DIRNAME,dirname,$PATH)dnl

AC_CYGWIN()dnl

if ${TEST} "${CYGWIN}" = "yes" ; then
    PATH_SEPARATOR="\\;"
else
    PATH_SEPARATOR=":"
fi

AC_SUBST(PATH_SEPARATOR)

AC_PROG_JAVATOOL(JAVA,java,[    Java runtime])
dnl AC_PROG_JAVATOOL(JAVAC,javac,[   Java compiler])
AC_PROG_JAVATOOL(JAVADOC,javadoc,[ Java documentation compiler])
dnl [You will not be able to create the Javadoc documentation])
dnl AC_PROG_JAVATOOL(JAR,jar,[     Java Archiver])

AC_PATH_JAVACLASS(ANT,jakarta,org.apache.tools.ant.Main,[     Where to find the Jakarta Ant jar file (http://jakarta.apache.org/ant/)])
AC_PATH_JAVACLASS(JAVA_COMPILER,jakarta,sun.tools.javac.Main,[     Where to find the Jakarta javac jar file (http://jakarta.apache.org/)])
AC_PATH_JAVACLASS(XERCES,jakarta,org.apache.xerces.parsers.DOMParser,[     Where to find the Xerces jar file (http://xml.apache.org/xerces-j/)])

PHIDGET_ENABLED=false

AC_MSG_CHECKING(if phidget is enabled)

AC_ARG_WITH(phidget,
[  --with-phidget          Add support for Phidget controller],
[
    AC_MSG_RESULT(yes)

    AC_ARG_WITH(jusb,
    [  --with-jusb             Specify the root directory of jUSB (http://jusb.sourceforge.net/)],
    [
        dnl Verify the library location
        
        AC_MSG_CHECKING(libjusb.so)
        
        if ${TEST} ! -f $withval/libjusb.so ; then
        
            AC_MSG_ERROR([jUSB shared library (libjusb.so) doesn't exist at ${withval}/libjusb.so])
        else
            JUSB_LIB=${withval}
            AC_SUBST(JUSB_LIB)
            AC_MSG_RESULT(${JUSB_LIB})
        fi
        
        dnl Verify the jar file location
        
        AC_PATH_JAVACLASS(JUSB,${withval},usb.core.Bus,[    Where to find the jUSB jar file (http://jusb.sourceforge.net/)])
        
    ])
    
    PHIDGET_ENABLED=true
],
[
    AC_MSG_RESULT(no)
])

AC_SUBST(PHIDGET_ENABLED)

TARGET_CLASSPATH=`cd ${srcdir} && pwd`/src/java

AC_PATH_EXTRA(ANT)
AC_PATH_EXTRA(JAVA_COMPILER)
AC_PATH_EXTRA(XERCES)

AC_SUBST(TARGET_CLASSPATH)dnl
AC_SUBST(JAVA_OPT)

AC_OUTPUT(
	Makefile
	build/Makefile
	build/build.sh
	build/.${PACKAGE}.properties
	src/Makefile
	src/scripts/Makefile
	src/scripts/run/Makefile
	src/scripts/run/ft_view
	src/scripts/test/Makefile
	src/scripts/test/ft_clock
	src/scripts/test/ft_swing
	src/scripts/test/sync_check
	src/java/Makefile
)dnl

if ${TEST} -z "$GLOBAL_OPTIONS_PRESENT" ; then
	AC_MSG_RESULT([You may want to create the ${GLOBAL_OPTIONS} file (see documentation)])
else
        AC_MSG_RESULT()
	AC_MSG_RESULT([Global options (${GLOBAL_OPTIONS}):])
	AC_MSG_RESULT()
	cat ${GLOBAL_OPTIONS}
	AC_MSG_RESULT()
fi

if ${TEST} -z "$LOCAL_OPTIONS_PRESENT" ; then
	AC_MSG_RESULT([You may want to create the ${LOCAL_OPTIONS} file (see documentation)])
else
        AC_MSG_RESULT()
	AC_MSG_RESULT([Local options (${LOCAL_OPTIONS}):])
	AC_MSG_RESULT()
	cat ${LOCAL_OPTIONS}
	AC_MSG_RESULT()
fi

AC_MSG_RESULT()

AC_MSG_RESULT(ServoMaster will be installed to:  ${prefix})
AC_MSG_RESULT(Configuration files are in:        ${prefix}/etc)
AC_MSG_RESULT(JDK_HOME and JAVA_HOME set to:     ${JDK_HOME})
AC_MSG_RESULT()
AC_MSG_RESULT(Build classpath set to:)

DISPLAY_CLASSPATH=`${ECHO} $TARGET_CLASSPATH|${TR} ":" "\n"`

AC_MSG_RESULT()

for ITEM in ${DISPLAY_CLASSPATH}; do
    AC_MSG_RESULT([        ${ITEM}])
done

AC_MSG_RESULT()

dnl Just to be sure the jar gets rebuilt in case we've changed the configuration

${RM} -f build/${PACKAGE}.jar
