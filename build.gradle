plugins {
    id 'com.github.spotbugs'
    id 'net.ltgt.errorprone' apply false
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'jacoco'
    apply plugin: 'net.ltgt.errorprone'

    sourceCompatibility = 11
    targetCompatibility = 11

    group = 'net.sf.servomaster'
    version = '3.0.0-SNAPSHOT'

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives javadocJar, sourcesJar
    }

    jacoco {
        toolVersion = "0.8.5"
    }

/*
    apply plugin: 'findbugs'

    findbugs {

        excludeFilter = file("$rootProject.projectDir/findbugs-exclude.xml")
        ignoreFailures = true
    }
 */
    repositories {

        jcenter()
    }

    dependencies {

        implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.13.2'
        implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.13.2'

        testImplementation 'junit:junit:4.13'

        errorprone 'com.google.errorprone:error_prone_core:2.6.0'
    }

    uploadArchives {
        repositories {
            mavenDeployer {

                // VT: FIXME: Later
                //beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: findProperty('ossrhUsername'), password: findProperty('ossrhPassword'))
                }

                snapshotRepository(url: "https://s01.oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: findProperty('ossrhUsername'), password: findProperty('ossrhPassword'))
                }

                pom.project {
                    name 'Servomaster'
                    packaging 'jar'
                    description 'Platform and hardware Independent servo controller driver'
                    url 'https://github.com/climategadgets/servomaster'

                    scm {
                        connection 'scm:git:https://github.com/climategadgets/servomaster.git'
                        developerConnection 'scm:git:https://github.com/climategadgets/servomaster.git'
                        url 'https://github.com/climategadgets/servomaster.git'
                    }

                    licenses {
                        license {
                            name 'GNU General Public License v3.0'
                            url 'https://www.gnu.org/licenses/gpl-3.0.en.html'
                        }
                    }

                    developers {
                        developer {
                            id 'vt'
                            name 'Vadim Tkachenko'
                            email 'vt@homeclimatecontrol.com'
                        }
                    }
                }
            }
        }
    }

    publishing {
        publications {
            maven(MavenPublication) {
            from components.java
            }
        }
    }
}
